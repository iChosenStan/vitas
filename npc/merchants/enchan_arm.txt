//===== rAthena Script ======================================= 
//= Armor Enchanter
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.3a
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Add a +1, +2, or +3 random stat to a specified armor.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//= 1.1 Corrected typo (Sit -> Suit) (bugreport:4586)
//= 1.2 Optimized. [Euphy]
//= 1.3 Fixed some typos and optimized a little more. [Daegaladh]
//= 1.3a Added 'disable_items' command. [Euphy]
//============================================================ 

prontera,147,162,5	script	Aprendiz de Artesão	559,{
	disable_items;
	mes "[Aprendiz de Artesão]";
	if (Zeny >= 400000) {
		mes "Estive estudando maneiras de aprimorar uma armadura para maximizar sua capacidade.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Enchanting é uma incrível habilidade que infunde poderes de status misteriosos no soquete escondido da armadura.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "No entanto, você deve ter em mente que, se houver duas armaduras do mesmo tipo em sua posse, o Encantamento será aplicado na ordem em que são colocados em seu inventário.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Nesse caso, o Encantamento pode ser aplicado a um item que você não quis encantar . Então, apenas traga ^5555ffuma armadura^000000 para que seja um encantamento seguro...";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Eu não sou responsável pelo que acontece se você tiver mais do que um item do mesmo tipo em seu inventário.";
		next;
		switch(select("[ ^FF0000•^000000 ] Armadura sem slot.:[ ^FF0000•^000000 ] Armadura com slot.:[ ^FF0000•^000000 ] Armadura especiais.:[ ^FF0000•^000000 ] Pack Guild.:[ ^FF0000•^000000 ] Talvez na próxima vez.")) {
		case 1:
			setarray .@Items[0],2307,2309,2314,2316,2321,2325,2327,2328,2330,2332,2334,2335,2341,2344,2346,2348,2350,2337,2386,2394,2395,2396;
			set .@j,50;
			break;
		case 2:
			setarray .@Items[0],2311,2318,2319,2320,2308,2310,2315,2317,2322,2324,2326,2331,2333,2336,2342,2345,2347,2349,2351;
			set .@j,55;
			set .@k,1;
			break;
		case 3:
			setarray .@Items[0],2364,2365,2391,2374,2375,2376,2377,2378,2379,2380,2381,2382,2387,2388,2389,2390;
			set .@j,60;
			break;
		case 4:
			setarray .@Items[0],20811,20812,20826;
			set .@j,60;
			break;
		case 5:
			mes "[Aprendiz de Artesão]";
			mes "Por favor, volte quando tiver interesse em encantar sua armadura.";
			close;
		}
		set .@menu$,"";
		for(set .@i,0; .@i<getarraysize(.@Items); set .@i,.@i+1)
			set .@menu$, .@menu$+getitemname(.@Items[.@i])+((.@k)?" [1]":"")+":";
		callsub S_EnchantArmor, .@Items[select(.@menu$)-1], .@j;
		end;
	}
	mes "Estou encarregado de Encantar armaduras";
	next;
	mes "[Aprendiz de Artesão]";
	mes "Se, por acaso, você quer encantar sua armadura, me traga ^FF0000400.000 zeny^000000 e a armadura que você quer encantar e você está pronto para ir.";
	close;

S_EnchantArmor:
	set .@itemid,getarg(0);
	set .@failrate,getarg(1);
	mes "[Aprendiz de Artesão]";
	if (countitem(.@itemid) == 1) {
		mes "O encantamento custará ^FF0000400.000 zeny^000000. E haverá uma opção aleatória encantada. Claro, há uma chance de quebrar sua armadura.";
		next;
		mes "[Aprendiz de Artesão]";
		mes "Primeiro e mais importante.";
		mes "^ff5555Nível de refinamento existente da armadura";
		mes "e cartas/runas serão deletados.^000000";
		mes "Você ainda quer tentar um encantamento?";
		next;
		if(select("Hmm ... Deixe-me pensar sobre isso.:Vamos lá.") == 1) {
			mes "[Aprendiz de Artesão]";
			mes "Bem, não posso culpá-lo. Segurança primeiro";
			mes "Tenha um bom dia.";
			close;
		}
		mes "[Aprendiz de Artesão]";
		mes "Muito aventureiro, vamo la!";
		close2;
		specialeffect2 EF_MAPPILLAR;
		if (Zeny < 400000) {
			mes "[Aprendiz de Artesão]";
			mes "Desculpe, mas você não tem ^FF0000zeny^000000 suficiente.";
			close;
		}
		progressbar "ffff00",7;
		set Zeny, Zeny-400000;
		delitem .@itemid,1;
		switch (rand(1,.@failrate)) {
			case 1: set .@addpart,4702;break;
			case 2: set .@addpart,4712;break;
			case 3: set .@addpart,4722;break;
			case 4: set .@addpart,4732;break;
			case 5: set .@addpart,4742;break;
			case 6: set .@addpart,4752;break;
			case 7:
			case 8: set .@addpart,4701;break;
			case 9:
			case 10: set .@addpart,4711;break;
			case 11:
			case 12: set .@addpart,4721;break;
			case 13:
			case 14: set .@addpart,4731;break;
			case 15:
			case 16: set .@addpart,4741;break;
			case 17:
			case 18: set .@addpart,4751;break;
			case 19:
			case 20:
			case 21: set .@addpart,4700;break;
			case 22:
			case 23:
			case 24: set .@addpart,4710;break;
			case 25:
			case 26:
			case 27: set .@addpart,4720;break;
			case 28:
			case 29:
			case 30: set .@addpart,4730;break;
			case 31:
			case 32:
			case 33: set .@addpart,4740;break;
			case 34:
			case 35:
			case 36: set .@addpart,4750;break;
			default:
				specialeffect2 EF_PHARMACY_FAIL;
				mes "[Aprendiz de Artesão]";
				mes "Bem, isso é muito ruim.";
				mes "O equipamento solicitado não conseguiu encantar.";
				close;
		}
		getitem2 .@itemid, 1, 1, 0, 0, 0, 0, 0, .@addpart;
	}
	else {
		mes "Hmm? Não há nada a ser encantado!";
		mes "Volte com apenas um equipamento para ser encantado.";
		close;
	}
}