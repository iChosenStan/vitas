//===== rAthena Script =======================================
//= Card Removal NPC
//===== By: ==================================================
//= TyrNemesis^
//===== Current Version: =====================================
//= 1.2a
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Removes cards from equipped items.
//===== Additional Comments: =================================
//= 1.0 First version. [TyrNemesis^]
//= 1.2 Optimized and fixed getequipname menu. [Kisuka]
//= 1.2a Added 'disable_items' command. [Euphy]
//============================================================

prt_in,28,73,4	script	Removedor Publico	78,{

	set .zenycost,200000;    // base cost of the card remover services (in Zeny)
	set .percardcost,25000;  // cost per card of the card remover services (in Zeny)
	set .faildestroy,1;      // should the card remover have a chance of failure that destroys items? (1=yes, 0=no)

	disable_items;
	mes "[Wise Old Woman]";
	mes "Bom dia, jovem. Eu tenho o poder de remover cartas que você tem composto em seu equipamento. Essa idéia te agrada?";
	next;
	switch(select("Sim, Vamos lá.:O que você cobra?:Não, obrigado.")) {
	case 1:
		mes "[Wise Old Woman]";
		mes "Muito bem. Qual item devo examinar para você?";
		next;

		setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
		for( set .@i,1; .@i <= 10; set .@i,.@i+1 ) {
			if( getequipisequiped(.@indices[.@i]) )
				set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			set .@menu$, .@menu$ + ":";
		}
		set .@part, .@indices[ select(.@menu$) ];
		if(!getequipisequiped(.@part)) {
			mes "[Wise Old Woman]";
			mes "Jovem ... Você não está vestindo nada para que eu possa remover cartas.";
			close;
		}
		if(getequipcardcnt(.@part) == 0) {
			mes "[Wise Old Woman]";
			mes "Jovem ... Não há cartas compostos neste item. Não posso fazer nada com isso, tenho medo.";
			close;
		}
		set .@cardcount,getequipcardcnt(.@part);
		
		if (!checkweight(1202,(.@cardcount+1))) { 
			mes "^3355FFSó um minuto!";
			mes "Não posso oferecer nenhum dos meus";
			mes "serviços para você porque";
			mes "você está carregando muito peso";
			mes "Coloque seus itens extras no";
			mes "Storage e venha novamente ~";
			close;
		}
		mes "[Wise Old Woman]";
		mes "This item has " + .@cardcount + " cartas combinados nele. Para realizar minha magia, eu precisarei" + (.zenycost+(.@cardcount * .percardcost)) + " zeny, a ^0000FFFragmento-Estelar^000000, e ^0000FFGema Amarela^000000.";
		next;
		if(select("Muito bem. Faça.:deixa pra lá.") == 2) {
			mes "[Wise Old Woman]";
			mes "Muito bem. Retornar de uma só vez se você procurar meus serviços.";
			close;
		}
		if((zeny < (.zenycost+(.@cardcount * .percardcost))) || (countitem(1000) < 1) || (countitem(715) < 1)) {
			mes "[Wise Old Woman]";
			mes "Você não tem todos os itens que eu preciso para trabalhar minha magia, criança. Venham novamente quando você tiver todos items.";
			close;
		}
		mes "[Wise Old Woman]";
		mes "Antes de começar, devo avisá-lo - posso falhar. Se eu fizer isso, eu posso destruir as cartas, o item ou ambos. Eu não dou reembolsos. Dito isto, o que é mais importante para você: as cartas ou o item?";
		next;
		switch(select("Eu mudei de idéia sobre isso.:Os items.:As Cartas.")) {
		case 1:
			mes "[Wise Old Woman]";
			mes "Muito bem. Volte de uma vez se você procurar meus serviços.";
			close;
		case 2:
			set .@failtype,1;
			break;
		case 3:
			set .@failtype,2;
		}
		mes "[Wise Old Woman]";
		mes "Muito bem. Vou começar.";
		set Zeny, Zeny - (.zenycost+(.@cardcount * .percardcost));
		delitem 1000,1; //Star_Crumb
		delitem 715,1; //Yellow_Gemstone
		
		// Replace the constants in the next 3 lines with failure chance values defined in refine_db.txt
		// First value = Total failure chance (item and cards destroyed)
		// Second value = Partial failure chance (one or the other is destroyed, player decides which one is safe)
		// Third value = Harmless failure chance (all that's lost is your investment)

		set .@failchance,rand(100);
		if (.faildestroy==1) {
			if(.@failchance < 2) {
				next;
				failedremovecards .@part,0;
				mes "[Wise Old Woman]";
				mes "O processo foi uma falha total. Receio que o item e as cartas tenham sido destruídos.";
				close;
			}

			if(.@failchance < 8) {
				if (.@failtype == 1) {
					next;
					failedremovecards .@part,1;
					mes "[Wise Old Woman]";
					mes "Enquanto eu consegui remover as cartas do item, elas foram destruídas no processo. O item, no entanto, está bem.";
					close;
				}

				if (.@failtype == 2) {
					next;
					failedremovecards .@part,2;
					mes "[Wise Old Woman]";
					mes "Muito infeliz. Consegui remover as cartas, mas o item em si foi destruído no processo.";
					close;
				}
			}
		}

		if(.@failchance < 10) {
			next;
			failedremovecards .@part,3;
			mes "[Wise Old Woman]";
			mes "Não consegui remover as cartas. Por sorte, no entanto, tanto o item quanto as cartas ainda estão bem.";
			close;
		}
		next;
		successremovecards .@part;
		mes "[Wise Old Woman]";
		mes "O processo foi um sucesso. Aqui estão as suas cartas e seu item.";
		close;
	case 2:
		mes "[Wise Old Woman]";
		mes "Eu cobro uma taxa fixa de "+callfunc("F_InsertComma",.zenycost)+" zeny, mais "+callfunc("F_InsertComma",.percardcost)+" zeny para cada carta que eu removo do item. Além disso, eu preciso de Fragmento-Estelar e uma Gema amarela para trabalhar minha magia.";
		close;
	case 3:
		mes "[Wise Old Woman]";
		mes "Muito bem. Retornar de uma só vez se você procurar meus serviços.";
		close;
	}
}
