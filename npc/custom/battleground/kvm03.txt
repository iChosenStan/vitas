//===== rAthena Script =======================================
//= BattleGround System - KvM 60-80
//===== Description: =========================================
//= [Official Conversion]
//= Kreiger Von Midgard Battleground for levels 60 to 80
//= - Winning Team: 2 points
//= - Losing Team: 1 point
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//= 1.2 Upated some announces and dialogs from iRO.
//=     Changed how the scoreboard works slightly.
//=     Removed the areapercentheals, and minor things.
//= 1.3 Fixed wrong names for disablenpc/enablenpc. [Ai4rei]
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,223,226,5	script	KVM Waiting Room#a3::KvM03R_Guillaume	858,{
	end;
OnInit:
	waitingroom "Campal 3x3",4,"KvM03_BG_Out::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@KvM03BG_id1, waitingroom2bg("bat_c03",52,129,"KvM03_BG::OnGuillaumeQuit","KvM03_BG::OnGuillaumeDie");
	end;
}

bat_room,223,205,0	script	KVM Waiting Room#b3::KvM03R_Croix	858,{
	end;
OnInit:
	waitingroom "Campal 3x3",4,"KvM03_BG_Out::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@KvM03BG_id2, waitingroom2bg("bat_c03",147,55,"KvM03_BG::OnCroixQuit","KvM03_BG::OnCroixDie");
	end;
}
bat_room,225,220,0	warp2	#kvm701	2,2,prontera,191,155
bat_room,225,211,0	warp2	#kvm702	2,2,prontera,191,155

// Starting Line
//============================================================
-	script	KVM03::CellEffect3	-1,{
	end;

OnKvM03One:
	specialeffect EF_CHANGECOLD;
	end;

OnKvM03Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c03,54,124,3	duplicate(CellEffect3)	#RedcellA1-3	139
bat_c03,55,124,3	duplicate(CellEffect3)	#RedcellA2-3	139
bat_c03,56,124,3	duplicate(CellEffect3)	#RedcellA3-3	139
bat_c03,57,124,3	duplicate(CellEffect3)	#RedcellA4-3	139
bat_c03,57,125,3	duplicate(CellEffect3)	#RedcellA5-3	139
bat_c03,57,126,3	duplicate(CellEffect3)	#RedcellA6-3	139
bat_c03,57,127,3	duplicate(CellEffect3)	#RedcellA7-3	139

bat_c03,145,59,3	duplicate(CellEffect3)	#RedcellB1-3	139
bat_c03,144,59,3	duplicate(CellEffect3)	#RedcellB2-3	139
bat_c03,143,59,3	duplicate(CellEffect3)	#RedcellB3-3	139
bat_c03,142,59,3	duplicate(CellEffect3)	#RedcellB4-3	139
bat_c03,142,56,3	duplicate(CellEffect3)	#RedcellB5-3	139
bat_c03,142,57,3	duplicate(CellEffect3)	#RedcellB6-3	139
bat_c03,142,58,3	duplicate(CellEffect3)	#RedcellB7-3	139

bat_c03,54,128,3	script	#A_camp_start03	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start03";
	end;
OnEnable:
	enablenpc "#A_camp_start03";
	end;
OnDisable:
	disablenpc "#A_camp_start03";
	end;
OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c03,146,56,3	script	#B_camp_start03	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start03";
	end;
OnEnable:
	enablenpc "#B_camp_start03";
	end;
OnDisable:
	disablenpc "#B_camp_start03";
	end;
OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM03_BG	-1,{
	end;

OnInit:
	setwall "bat_c03",54,122,6,7,0,"batc03wall_a";
	setwall "bat_c03",55,122,5,7,0,"batc03wall_b";
	setwall "bat_c03",140,56,6,7,0,"batc03wall_c";
	setwall "bat_c03",140,57,5,7,0,"batc03wall_d";
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM03BG == 2 ) {
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c03",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 )
			donpcevent "KvM03_BG::OnCroixWin";
		else {
			mapannounce "bat_c03", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c03", "O numero de jogadores do time Vermelho é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM03BG == 2 ) {
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c03",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 )
			donpcevent "KvM03_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c03", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c03", "O numero de jogadores do time Vermelho é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnStart:
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	set $@KvM03BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM03BG_id1,"bat_c03",53,128;
	bg_warp $@KvM03BG_id2,"bat_c03",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c03", "Preparem a batalha Campal RagnaTwo vai começar em 30 segundos.",bc_map,"0x00ff00";
	mapannounce "prontera", "Preparem a batalha Campal RagnaTwo vai começar em 30 segundos.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c03", "O tempo maximo da batalha é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c03", "Prepare-se para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Você pode ajudar seus amigos com buffs.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start03::OnEnable";
	donpcevent "#B_camp_start03::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start03::OnDisable";
	donpcevent "#B_camp_start03::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c03", "Começou boa sorte, jogadores afk poderão ser punidos",bc_map,"0x00ff00";
	donpcevent "::OnKvM03Two";
	end;

OnTimer31000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM03BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM03BG_id2, 0);
	if( .Guillaume_Count < 3 || .Croix_Count < 3 )
	{
		set $@KvM03BG_Victory, 3;
		set $@KvM03BG, 3;
		mapannounce "bat_c03","Não há jogadores suficientes para iniciar a batalha",1,0xC0C0C0;
		stopnpctimer;
		donpcevent "KvM03_BG::OnStop";
		end;
	}
	set $@KvM03BG, 2; // Playing
	bg_warp $@KvM03BG_id1,"bat_c03",62,119;
	bg_warp $@KvM03BG_id2,"bat_c03",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c03", "1 minuto restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c03", "30 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c03", "15 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c03", "10 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c03", "5 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c03", "A KVM terminou Ragna-Two.",bc_map,"0x00ff00";
	mapannounce "prontera", "A KVM terminou Ragna-Two.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM03_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM03_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM03BG, 3;
		set $@KvM03BG_Victory, 3;
		mapannounce "bat_c03", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c03", "O numero de jogadores do time vermelho é  "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c03", "Uma batalha acabou em um empate",bc_map,"0x00ff00";
		donpcevent "KvM03_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM03BG, 3;
	set $@KvM03BG_Victory, 1;
	mapannounce "bat_c03", "O Time Azul Ganhou !!",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Parabéns aos membros do time azul.",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Todos serão movidos para o ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM03_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM03BG, 3;
	set $@KvM03BG_Victory, 2;
	mapannounce "bat_c03", "O Time Vermelho Ganhou !!",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Parabéns aos membros do time vermelho.",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Todos serão movidos para o ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM03_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM03A";
	enablenpc "KVM Officer#KVM03B";
	// Warp Teams
	bg_warp $@KvM03BG_id1,"bat_c03",53,128;
	bg_warp $@KvM03BG_id2,"bat_c03",146,55;
	donpcevent "KvM03_BG_Out::OnBegin";
	end;
}

-	script	KvM03_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c03", "Vejo que foi um forte guerreiro",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c03", "O Oficial concederá o premio por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Você tem 30 segundos !!",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c03", "A menos que você fale com o oficial, você não pode ganhar os pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c03", "Por favor, seja cuidadoso.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c03", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set getvariableofnpc(.Croix_Count,"KvM03_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM03_BG"), 0;
	set $@KvM03BG_Victory, 0;
	if( $@KvM03BG_id1 ) { bg_destroy $@KvM03BG_id1; set $@KvM03BG_id1, 0; }
	if( $@KvM03BG_id2 ) { bg_destroy $@KvM03BG_id2; set $@KvM03BG_id2, 0; }
	disablenpc "KVM Officer#KVM03A";
	disablenpc "KVM Officer#KVM03B";
	mapwarp "bat_c03","prontera",191,155;
	maprespawnguildid "bat_c03",0,3; // Just in case someone else
	bg_updatescore "bat_c03",3,3;
	set $@KvM03BG, 0;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM03BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM03R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM03R_Croix");

	if( .@Guillaume < 3 || .@Croix < 3 )
		end;

	set $@KvM03BG, 1; // Starting
	donpcevent "KvM03R_Croix::OnEnterBG";
	donpcevent "KvM03R_Guillaume::OnEnterBG";
	donpcevent "KvM03_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c03,51,130,5	script	KVM Officer#KVM03A	419,{
	if( $@KvM03BG_Victory ) {
		mes "[KVM Officer]";
		if( $@KvM03BG_Victory == Bat_Team ) {
			mes "Good Game.";
			mes "Que a glória da KVM esteja com você.";
			mes "Você foi um vencedor receba seu premio.";
			close2;
			getitem 7773,40;
			getitem 7828,30;
			getitem 7829,30;
			getitem 672,1;
		}
		else {
			mes "Eu sinto muitíssimo.";
			mes "Desejo-lhe melhor sorte na próxima vez.";
			mes "Receba seu prémio de participação";
			close2;
			getitem 7773,20;
			getitem 7828,15;
			getitem 7773,15;
		}
		bg_leave;
		set Bat_Team,0;
		warp "prontera",191,155;
	}
	end;
}
bat_c03,148,53,1	duplicate(KVM Officer#KVM03A)	KVM Officer#KVM03B	415
