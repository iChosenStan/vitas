//===== rAthena Script =======================================
//= BattleGround System - KvM 1-59
//===== Description: =========================================
//= [Official Conversion]
//= Kreiger Von Midgard Battleground for levels 1 to 59
//= - Winning Team: 1 point
//= - Losing Team: 0 point
//===== Changelogs: ==========================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Updated using official Aegis files. [L0ne_W0lf]
//= 1.2 Upated some announces and dialogs from iRO.
//=     Changed how the scoreboard works slightly.
//=     Removed the areapercentheals, and minor things.
//= 1.3 Fixed wrong names for disablenpc/enablenpc. [Ai4rei]
//=     Fixed points in text and actual points differing.
//============================================================

// Waiting Room NPCs
//============================================================
bat_room,228,226,5	script	KVM Waiting Room#a2::KvM02R_Guillaume	858,{
	end;
OnInit:
	waitingroom "Campal 6x6",7,"KvM02_BG_Out::OnGuillaumeJoin",1;
	end;
OnEnterBG:
	set $@KvM02BG_id1, waitingroom2bg("bat_c02",52,129,"KvM02_BG::OnGuillaumeQuit","KvM02_BG::OnGuillaumeDie");
	end;
}

bat_room,228,205,0	script	KVM Waiting Room#b2::KvM02R_Croix	858,{
	end;
OnInit:
	waitingroom "Campal 6x6",7,"KvM02_BG_Out::OnCroixJoin",1;
	end;
OnEnterBG:
	set $@KvM02BG_id2, waitingroom2bg("bat_c02",147,55,"KvM02_BG::OnCroixQuit","KvM02_BG::OnCroixDie");
	end;
}

bat_room,197,220,0	warp2	#kvm601	2,2,prontera,191,155
bat_room,197,21,0	warp2	#kvm602	2,2,prontera,191,155

// Starting Line
//============================================================
-	script	KVM02::CellEffect2	-1,{
	end;
OnKvM02One:
	specialeffect EF_CHANGECOLD;
	end;
OnKvM02Two:
	specialeffect EF_CHANGEPOISON;
	end;
}

bat_c02,54,124,3	duplicate(CellEffect2)	#RedcellA1-2	139
bat_c02,55,124,3	duplicate(CellEffect2)	#RedcellA2-2	139
bat_c02,56,124,3	duplicate(CellEffect2)	#RedcellA3-2	139
bat_c02,57,124,3	duplicate(CellEffect2)	#RedcellA4-2	139
bat_c02,57,125,3	duplicate(CellEffect2)	#RedcellA5-2	139
bat_c02,57,126,3	duplicate(CellEffect2)	#RedcellA6-2	139
bat_c02,57,127,3	duplicate(CellEffect2)	#RedcellA7-2	139

bat_c02,145,59,3	duplicate(CellEffect2)	#RedcellB1-2	139
bat_c02,144,59,3	duplicate(CellEffect2)	#RedcellB2-2	139
bat_c02,143,59,3	duplicate(CellEffect2)	#RedcellB3-2	139
bat_c02,142,59,3	duplicate(CellEffect2)	#RedcellB4-2	139
bat_c02,142,56,3	duplicate(CellEffect2)	#RedcellB5-2	139
bat_c02,142,57,3	duplicate(CellEffect2)	#RedcellB6-2	139
bat_c02,142,58,3	duplicate(CellEffect2)	#RedcellB7-2	139

bat_c02,54,128,3	script	#A_camp_start02	139,4,4,{
	end;
OnInit:
	disablenpc "#A_camp_start02";
	end;
OnEnable:
	enablenpc "#A_camp_start02";
	end;
OnDisable:
	disablenpc "#A_camp_start03";
	end;
OnTouch:
	set Bat_Team,1;
	setquest 6025;
	end;
}

bat_c02,146,56,3	script	#B_camp_start02	139,4,4,{
	end;
OnInit:
	disablenpc "#B_camp_start02";
	end;
OnEnable:
	enablenpc "#B_camp_start02";
	end;
OnDisable:
	disablenpc "#B_camp_start02";
	end;
OnTouch:
	set Bat_Team,2;
	setquest 6025;
	end;
}

// Battleground Engine
//============================================================
-	script	KvM02_BG	-1,{
	end;

OnInit:
	setwall "bat_c02",54,122,6,7,0,"batc02wall_a";
	setwall "bat_c02",55,122,5,7,0,"batc02wall_b";
	setwall "bat_c02",140,56,6,7,0,"batc02wall_c";
	setwall "bat_c02",140,57,5,7,0,"batc02wall_d";
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	end;

OnGuillaumeQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnGuillaumeDie:
	if( $@KvM02BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Guillaume_Count < 1 ) donpcevent "KvM02_BG::OnCroixWin";
		else {
			mapannounce "bat_c02", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O numero de jogadores do time Vermelho é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnCroixQuit:
	//set BG_Delay_Tick, gettimetick(2) + 1200;
OnCroixDie:
	if( $@KvM02BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		bg_updatescore "bat_c02",.Guillaume_Count,.Croix_Count;
		if( .Croix_Count < 1 ) donpcevent "KvM02_BG::OnGuillaumeWin";
		else {
			mapannounce "bat_c02", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
			mapannounce "bat_c02", "O numero de jogadores do time Vermelho é "+.Croix_Count+".",bc_map,"0x00ff00";
		}
	}
	end;

OnStart:
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	set $@KvM02BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Preparem a batalha Campal RagnaTwo vai começar em 30 segundos.",bc_map,"0x00ff00";
	mapannounce "prontera", "Preparem a batalha Campal RagnaTwo vai começar em 30 segundos.",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O tempo maximo da batalha é 5 minutos.",bc_map,"0x00ff00";
	end;

OnTimer6000:
	mapannounce "bat_c02", "Prepare-se para a batalha KVM.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Você pode ajudar seus amigos com buffs.",bc_map,"0x00ff00";
	donpcevent "#A_camp_start02::OnEnable";
	donpcevent "#B_camp_start02::OnEnable";
	end;

OnTimer13000:
	donpcevent "#A_camp_start02::OnDisable";
	donpcevent "#B_camp_start02::OnDisable";
	end;

OnTimer30000:
	mapannounce "bat_c02", "Começou boa sorte, jogadores afk poderão ser punidos",bc_map,"0x00ff00";
	donpcevent "::OnKvM02Two";
	end;


OnTimer31000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM02BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM02BG_id2, 0);
	if( .Guillaume_Count < 6 || .Croix_Count < 6 )
	{
		set $@KvM02BG_Victory, 3;
		set $@KvM02BG, 3;
		mapannounce "bat_c02","Não há jogadores suficientes para iniciar a batalha",1,0xC0C0C0;
		stopnpctimer;
		donpcevent "KvM02_BG::OnStop";
		end;
	}
	set $@KvM02BG, 2; // Playing
	bg_warp $@KvM02BG_id1,"bat_c02",62,119;
	bg_warp $@KvM02BG_id2,"bat_c02",137,64;
	end;

OnTimer300000:
	mapannounce "bat_c02", "1 minuto restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer330000:
	mapannounce "bat_c02", "30 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer345000:
	mapannounce "bat_c02", "15 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer350000:
	mapannounce "bat_c02", "10 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer355000:
	mapannounce "bat_c02", "5 segundos restante para terminar a batalha KVM.",bc_map,"0x00ff00";
	end;

OnTimer360000:
	mapannounce "bat_c02", "A KVM terminou Ragna-Two.",bc_map,"0x00ff00";
	mapannounce "prontera", "A KVM terminou Ragna-Two.",bc_map,"0x00ff00";
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM02_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM02_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM02BG, 3;
		set $@KvM02BG_Victory, 3;
		mapannounce "bat_c02", "O numero de jogadores do time azul é "+.Guillaume_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "O numero de jogadores do time vermelho é  "+.Croix_Count+".",bc_map,"0x00ff00";
		mapannounce "bat_c02", "Uma batalha acabou em um empate",bc_map,"0x00ff00";
		donpcevent "KvM02_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 1;
	mapannounce "bat_c02", "O Time Azul Ganhou !!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros do time azul.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão movidos para o ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM02BG, 3;
	set $@KvM02BG_Victory, 2;
	mapannounce "bat_c02", "O Time Vermelho Ganhou !!",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Parabéns aos membros do time vermelho.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Todos serão movidos para o ponto inicial.",bc_map,"0x00ff00";
	donpcevent "KvM02_BG::OnStop";
	end;

OnStop:
	stopnpctimer;
	enablenpc "KVM Officer#KVM02A";
	enablenpc "KVM Officer#KVM02B";
	// Warp Teams
	bg_warp $@KvM02BG_id1,"bat_c02",53,128;
	bg_warp $@KvM02BG_id2,"bat_c02",146,55;
	donpcevent "KvM02_BG_Out::OnBegin";
	end;
}

-	script	KvM02_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer1000:
	mapannounce "bat_c02", "Vejo que foi um forte guerreiro",bc_map,"0x00ff00";
	end;

OnTimer3000:
	mapannounce "bat_c02", "O Oficial concederá o premio por 30 segundos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Você tem 30 segundos !!",bc_map,"0x00ff00";
	end;

OnTimer5000:
	mapannounce "bat_c02", "A menos que você fale com o oficial, você não pode ganhar os pontos.",bc_map,"0x00ff00";
	mapannounce "bat_c02", "Por favor, seja cuidadoso.",bc_map,"0x00ff00";
	end;

OnTimer55000:
	mapannounce "bat_c02", "Você será enviado de volta.",bc_map,"0x00ff00";
	end;

OnTimer60000:
	stopnpctimer;
	set getvariableofnpc(.Croix_Count,"KvM02_BG"), 0;
	set getvariableofnpc(.Guillaume_Count,"KvM02_BG"), 0;
	set $@KvM02BG_Victory, 0;
	if( $@KvM02BG_id1 ) { bg_destroy $@KvM02BG_id1; set $@KvM02BG_id1, 0; }
	if( $@KvM02BG_id2 ) { bg_destroy $@KvM02BG_id2; set $@KvM02BG_id2, 0; }
	disablenpc "KVM Officer#KVM02A";
	disablenpc "KVM Officer#KVM02B";
	mapwarp "bat_c02","prontera",191,155;
	maprespawnguildid "bat_c02",0,3; // Just in case someone else
	bg_updatescore "bat_c02",6,6;
	set $@KvM02BG, 0;

OnGuillaumeJoin:
OnCroixJoin:
	if( $@KvM02BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM02R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM02R_Croix");

	if( .@Guillaume < 6 || .@Croix < 6 )
		end;

	set $@KvM02BG, 1; // Starting
	donpcevent "KvM02R_Croix::OnEnterBG";
	donpcevent "KvM02R_Guillaume::OnEnterBG";
	donpcevent "KvM02_BG::OnStart";
	end;
}

// Battleground rewards
//============================================================
bat_c02,51,130,5	script	KVM Officer#KVM02A	419,{
	if( $@KvM02BG_Victory ) {
		mes "[KVM Officer]";
		if( $@KvM02BG_Victory == Bat_Team ) {
			mes "Good Game.";
			mes "Que a glória da KVM esteja com você.";
			mes "Você foi um vencedor receba seu premio.";
			close2;
			getitem 7773,60;
			getitem 7828,40;
			getitem 7829,40;
			getitem 672,1;
		}
		else {
			mes "Eu sinto muitíssimo.";
			mes "Desejo-lhe melhor sorte na próxima vez.";
			mes "Receba seu prémio de participação";
			close2;
			getitem 7773,30;
			getitem 7828,20;
			getitem 7773,20;
		}
		bg_leave;
		set Bat_Team,0;
		warp "prontera",191,155;
		end;
	}
}
bat_c02,148,53,1	duplicate(KVM Officer#KVM02A)	KVM Officer#KVM02B	415
